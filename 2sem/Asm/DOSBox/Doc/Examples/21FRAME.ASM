;=============================================================================
;                 Задание # 21. Файл 21frame.asm
;      Резидентная система помощи - библиотека рисования рамок
;               Copyright (c) 2002 Andreev Andrey
;=============================================================================

;-----------------------------------------------(c) Andreev Andrey, 2002--;

;=============================================================================
; Процедуры рисования рамки с координатами x1 = bl; x2 = bh; y1 = dl; y2 = dh
;=============================================================================

;=============================================================================
; Главная процедура
;-----------------------------------------------------------------------------
; assumes :     es = 0b800h, cld.
; entry :       bl = x1, bh = x2, dl = y1, dh = y2.
; destr :       ax, di.
;=============================================================================
; ╔════════════════════════════ Константы ════════════════════════════╗
;------------------------------------------------------------------------
color           = 0eh
probel          = 00h         ; символ, которым заполняется прямоугольник

left_up         = 0c9h         ; ╔             символы рамки          -->
hor_line        = 0cdh         ; ═
right_up        = 0bbh         ; ╗
ver_line        = 0bah         ; ║
right_down      = 0bch         ; ╝
left_down       = 0c8h         ; ╚             символы рамки          <--
;------------------------------------------------------------------------
; ╚════════════════════════════ Константы ════════════════════════════╝
draw_frame      proc

                call address_x1_y1      ; вычисление di = (dl*80+bl)*2
; Первый символ - левый верхний угол рамки
                mov ah, color
                mov al, left_up
                stosw
; Цикл вывода верхней линии рамки
                call delta_x
                mov al, hor_line
                rep stosw
; Правый верхний угол рамки
                mov al, right_up
                stosw
; Вывод вертикальных линий рамки
                call drfr_new_line
; Вывод последней сточки
                mov al, left_down
                stosw
                call delta_x
                mov al, hor_line
                rep stosw
                mov al, right_down
                stosw

                ret
                endp
;-----------------------------------------------------------------------------
; Конец процедуры рисования рамки
;-----------------------------------------------------------------------------

;=============================================================================
; Процедура вычисления адреса координаты (x1; y1) по ф-ле  (y1*80+x1)*2
;-----------------------------------------------------------------------------
; entry :       dl = y1; bl = x1;
; exit :        di = (y1 * 80 + x1) * 2
; destr :       ax, di.
;=============================================================================
address_x1_y1   proc

                mov ah, 80d
                mov al, dl
                mul ah          ; ax = y1 * 80
                add al, bl      ; ax = y1 * 80 + x1
                shl ax, 1d      ; ax = (y1 * 80 + x1) * 2
                mov di, ax

                ret
                endp
;-----------------------------------------------------------------------------
; Конец процедуры вычисления адреса
;-----------------------------------------------------------------------------

;=============================================================================
; Процедура вычисления cx = x2 - x1.
;-----------------------------------------------------------------------------
; entry :       bl = x1; bh = x2;
; exit :        cx = bh - bl
; destr :       none.
;=============================================================================
delta_x         proc

                mov ch, 00h
                mov cl, bh
                sub cl, bl
                sub cl, 1d

                ret
                endp
;=============================================================================
; Процедура рисования линии типа '║               ║'
;-----------------------------------------------------------------------------
; entry :       di - текущий адрес
; assumes :     cld, ah - color
; exit :        di = di + 79*2 - x2 - x1
;-----------------------------------------------------------------------------
drfr_new_line   proc

                mov al, ver_line

                xchg bx, dx
                call delta_x
                xchg bx, dx
                push dx
; вычесление того, что надо добавить к di для перевода строки
                mov dx, 79d
                sub dl, bh
                add dl, bl
                shl dx, 1d

; цикл вывода строк "║       ║"
new_line_next:  add di, dx      ; перевод строки (  di = di+(79-x2+x1)*2  )
                stosw
                push cx
                call delta_x
                mov al, probel
                rep stosw
                pop cx
                mov al, ver_line
                stosw
                loop new_line_next

                add di, dx
                pop dx
                ret
                endp
;=============================================================================
; Конец блока процедур рисования рамки
;=============================================================================